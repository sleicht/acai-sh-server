defmodule AcaiWeb.UserRegistrationControllerTest do
  use AcaiWeb.ConnCase, async: false

  import Acai.AccountsFixtures

  describe "GET /users/register" do
    test "renders registration page", %{conn: conn} do
      conn = get(conn, ~p"/users/register")
      response = html_response(conn, 200)
      assert response =~ "Register"
      assert response =~ ~p"/users/log-in"
      assert response =~ ~p"/users/register"
    end

    test "redirects if already logged in", %{conn: conn} do
      conn = conn |> log_in_user(user_fixture()) |> get(~p"/users/register")

      # team-list.MAIN.3
      assert redirected_to(conn) == ~p"/teams"
    end
  end

  describe "POST /users/register" do
    @tag :capture_log
    test "creates account but does not log in", %{conn: conn} do
      email = unique_user_email()

      conn =
        post(conn, ~p"/users/register", %{
          "user" => valid_user_attributes(email: email)
        })

      refute get_session(conn, :user_token)
      assert redirected_to(conn) == ~p"/users/log-in"

      assert conn.assigns.flash["info"] =~
               ~r/An email was sent to .*, please access it to confirm your account/
    end

    @tag :capture_log
    test "email-delivery.SMTP.4: redirects with an error when email delivery fails", %{conn: conn} do
      previous_mailer_config = Application.get_env(:acai, Acai.Mailer)
      Application.put_env(:acai, Acai.Mailer, adapter: Acai.Support.FailingMailerAdapter)

      on_exit(fn ->
        Application.put_env(:acai, Acai.Mailer, previous_mailer_config)
      end)

      conn =
        post(conn, ~p"/users/register", %{
          "user" => valid_user_attributes(email: unique_user_email())
        })

      assert redirected_to(conn) == ~p"/users/log-in"
      assert conn.assigns.flash["error"] =~ "could not send the confirmation email"
    end

    test "render errors for invalid data", %{conn: conn} do
      conn =
        post(conn, ~p"/users/register", %{
          "user" => %{"email" => "with spaces"}
        })

      response = html_response(conn, 200)
      assert response =~ "Register"
      assert response =~ "must have the @ sign and no spaces"
    end
  end
end
